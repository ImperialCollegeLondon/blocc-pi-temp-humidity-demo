plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'application'
}

group 'uk.ac.ic.doc.blocc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.hyperledger.fabric:fabric-gateway:1.2.2'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-core:5.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    compileOnly 'io.grpc:grpc-api:1.54.1'
}

checkstyle {
    toolVersion '10.11.0'
    configFile rootProject.file("config/checkstyle/google_checks.xml")
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['uk/ac/ic/doc/blocc/BloccContractGateway.*', 'uk/ac/ic/doc/blocc/example/**'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['uk/ac/ic/doc/blocc/BloccContractGateway.*', 'uk/ac/ic/doc/blocc/example/**'])
        }))
    }

    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestCoverageVerification
installDist.dependsOn check