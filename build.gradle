plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'application'
}

group 'uk.ac.ic.doc.blocc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.hyperledger.fabric:fabric-gateway:1.2.2'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-core:5.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.55.1'
}

application {
    mainClass = 'uk.ac.ic.doc.blocc.Main'
}

checkstyle {
    toolVersion '10.11.0'
    configFile rootProject.file("config/checkstyle/google_checks.xml")
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestCoverageVerification
installDist.dependsOn check